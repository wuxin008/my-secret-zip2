#version 450
#extension GL_ARB_shading_language_include : require
#include "simulation_header.glsl"

layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

// prevent neighbors from interface->fluid cells to become/be gas cells
void main()
{
    uvec3 globalSize = gl_NumWorkGroups * gl_WorkGroupSize;
    uint index = gl_GlobalInvocationID.z * globalSize.x * globalSize.y + gl_GlobalInvocationID.y * globalSize.x + gl_GlobalInvocationID.x;
    if (index >= ubo.Nxyz) return;

    const uint flagsn_sus = flags[index]&(TYPE_SU|TYPE_S); // extract SURFACE flags
	if(flagsn_sus==TYPE_IF) { // flag interface->fluid is set
		uint nbs[Q]; // neighbor indices
		neighbors(index, nbs); // calculate neighbor indices
		for(uint q=1u; q<Q; q++) {
			const uint flagsji = flags[nbs[q]];
			const uint flagsji_su = flagsji&(TYPE_SU|TYPE_S); // extract SURFACE flags
			const uint flagsji_r = flagsji&~TYPE_SU; // extract all non-SURFACE flags
			if(flagsji_su==TYPE_IG) flags[nbs[q]] = flagsji_r|TYPE_I; // prevent interface neighbor cells from becoming gas
			else if(flagsji_su==TYPE_G) flags[nbs[q]] = flagsji_r|TYPE_GI; // neighbor cell was gas and must change to interface
		}
	}
}
// possible types at the end of surface_1(): TYPE_F / TYPE_I / TYPE_G / TYPE_IF / TYPE_IG / TYPE_GI